
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументРезультат.Очистить();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	// Флаг проверки пустой ли отчет
	Если НЕ Настройки.ДополнительныеСвойства.Свойство("ОтчетПустой") Тогда
		Настройки.ДополнительныеСвойства.Вставить("ОтчетПустой", Истина);
	Иначе
		Настройки.ДополнительныеСвойства.ОтчетПустой = Истина;	
	КонецЕсли;	
	
	ВнешниеНаборыДанных = Новый Структура;
	//ВнешниеНаборыДанных.Вставить("ТекстФайла", ТекстФайла());
	Если ДеньНедели(ТекущаяДата()) = 1 Тогда  //для понедельника за три дня смотрим
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
			"ДатаНачала", НачалоДня(ТекущаяДата()-3*86400));
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
			"ДатаКонца", КонецДня(ТекущаяДата()-86400));	
	Иначе
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
			"ДатаНачала", НачалоДня(ТекущаяДата()-86400));
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(
			"ДатаКонца", КонецДня(ТекущаяДата()-86400));	
	КонецЕсли;
	НастройкиОтчета = Настройки;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.НачатьВывод();
	ЭлементРезультата = ПроцессорКомпоновки.Следующий();
	Пока ЭлементРезультата <> Неопределено Цикл
		ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		// !!! Определить не пустой результат !!!
		// По умолчанию отчет считается пустым
		Если Настройки.ДополнительныеСвойства.ОтчетПустой Тогда
			// Обходим значения параметров текущего элемента компоновки
			// Если один из них заполнен, то значит отчет НЕ пустой
            Для Каждого ЗначениеПараметраМакетаКД Из ЭлементРезультата.ЗначенияПараметров Цикл
				Попытка
					Если ЗначениеЗаполнено(ЗначениеПараметраМакетаКД.Значение) Тогда
                    	Настройки.ДополнительныеСвойства.ОтчетПустой = Ложь;
						Прервать;
					КонецЕсли;
				Исключение 
				КонецПопытки;
            КонецЦикла;
		КонецЕсли;
		// !!! Определить не пустой результат !!!		
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();
	КонецЦикла;
	ПроцессорВывода.ЗакончитьВывод();
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	// Установим реквизит отчета, чтобы флаг пустой отчет или нет был виден на форме
	//Настройки.ДополнительныеСвойства.Свойство("ОтчетПустой", ЭтотОбъект.ОтчетПустой);	
	
КонецПроцедуры

#КонецОбласти
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли